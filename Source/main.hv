
gQuit := false

gDisableMouse := false

DebugLog := !(char^ frmt,...) -> void
{
	vars := int[32]
	va_start(vars&)
	if $posix
		vprintf(frmt,vars&)
	if $win32
		vprintf(frmt,vars&->{void^^}^)
	va_end(vars&)
}


fpsCounter := 0
lastCheckedTime := 0.0

resizeState := false
preQuit := false
main := !(int argc, char^^ argv) -> int
{
	gRepo.AddZipRoot("Dlls.zip")

	if ToolMain(argc,argv)
		return 0

	TExpectWorkers(1)
	vPreInit()

	CreateWindow(gWinStartW,gWinStartH)
	defer DestroyWindow() 

	InitVulkan()
	defer DestroyVulkan()

	vInit()
	
	gCam.camPos = vec4f(0.0f,0.0f,0.0f,1.0f)
	gCam.upDownAng = 0.0f
	gCam.leftRightAng = 0.0f

	VoidAudioInit()

	gPlayerMap = vGetMap("FirstMap")

	TSpawnTask(() ==> {
		prevTime := glfwGetTime()
		while true
		{
			FlushTempMemory()
			if gQuit {
				return void
			}
			nowTime := glfwGetTime()
			deltaTime := nowTime - prevTime
			vPhysStage(deltaTime)
			prevTime = nowTime
			TSleep(0.005)
		}
	})

	TSpawnTask( () ==> {
		
		prevTime := glfwGetTime()
		
		while true
		{
			FlushTempMemory()
			glfwPollEvents()

			nowTime := glfwGetTime()

			//if resizeState 
			//{
			//	if nowTime - gLastTimeResized > 1.0
			//	{
			//		CreateSwapchain(gWindowW,gWindowH)
			//		gCam.UpdatePerspective(gWindowW->{float},gWindowH->{float})
			//		resizeState = false
			//	}else{
			//		yield void
			//	}
			//}

			if glfwWindowShouldClose(glfwWindow)
			{
				preQuit = true
				return void
			}
			TSleep(0.01)
		}
	})

	prevTime := glfwGetTime()

	while  true 
	{
		if gQuit {
			return 0
		}
		FlushTempMemory()
		TAwaitWork(() ==>{
			DrawGetImage()
		})
		StartDraw()
		nowTime := glfwGetTime()
		gNowTime = nowTime
		deltaTime := nowTime - prevTime
		
		prevTime = nowTime
		gCam.InputCheck(deltaTime)
		gCam.BindDescriptor(mainCmd.Get())
		
		vDraw()
	
		StopDraw()

		if preQuit
		{
			//Quit()
			gQuit = true
			return 0
		}

		gHotloadCheck()
		fpsCounter++

	}
		
	return 0

}

